apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "de.memorian.template"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        resConfigs 'de', 'en'
    }

    buildTypes {

        debug {
            applicationIdSuffix ".debug"

            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

// Add generated source from UseKase
android.applicationVariants.all { variant ->
    variant.addJavaSourceFoldersToModel()
    def kotlinGenerated = file("$buildDir/generated/source/kaptKotlin/${variant.name}")
    variant.addJavaSourceFoldersToModel(kotlinGenerated)
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support Libraries
    def supportLibVersion = "27.1.1"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support.constraint:constraint-layout:1.1.0"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation 'com.pawegio.kandroid:kandroid:0.8.7@aar'
    // requires support lib 28
    //implementation 'androidx.core:core-ktx:1.0.0-alpha1'

    // RxJava/RxAndroid
    implementation "io.reactivex.rxjava2:rxjava:2.1.12"
    implementation("io.reactivex.rxjava2:rxandroid:2.0.1", {
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
    })
    implementation("io.reactivex.rxjava2:rxkotlin:2.2.0", {
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
    })

    def useKaseVersion = "0.0.2"
    implementation "guru.stefma.cleancomponents:usekase:$useKaseVersion"
    kapt  "guru.stefma.cleancomponents:usekase-processor:$useKaseVersion"

    // Timber
    implementation "com.jakewharton.timber:timber:4.7.0"

    // DI
    implementation "org.koin:koin-android:0.9.3"

    // ThirtyInch
    def thirtyinchVersion = "0.8.5"
    implementation "net.grandcentrix.thirtyinch:thirtyinch:$thirtyinchVersion"
    implementation "net.grandcentrix.thirtyinch:thirtyinch-rx2:$thirtyinchVersion"

    // Networking
    def retrofitVersion = "2.4.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:3.10.0"

    // View utility
    implementation "com.github.bumptech.glide:glide:4.6.1"

    // Room
    def roomVersion = "1.1.0"
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    implementation "android.arch.persistence.room:rxjava2:$roomVersion"
    kapt  "android.arch.persistence.room:compiler:$roomVersion"

    // Unit tests
    testImplementation "junit:junit:4.12"
    testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
}
